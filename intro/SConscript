#!/usr/bin/env python
# -*- coding: utf-8 -*-

import os.path

Import('env')

## A builder for the pdf and pdf_tex files generated by inkscape from
## svg files.  These are necessary when there's text in the figures and
## we want LaTeX to handle the text (otherwise, a PDF would be enough).
def add_tex_to_target(target, source, env):
  """inkscape called with --export-latex will create another file with
     _tex appended so add it to lisr of targets."""
  target.append(str(target[0]) + "_tex")
  return target, source

pdf_tex_bld = Builder(action='inkscape -Dz --export-latex -A $TARGET $SOURCE',
                      suffix='.pdf',
                      src_suffix='.svg',
                      emitter=add_tex_to_target)
env.Append(BUILDERS = {'PDF_TEX' : pdf_tex_bld})


def path4figure(fname=""):
  return os.path.join("figures", fname)

def path4data(fname=""):
  return os.path.join("data", fname)

def path4script(fname=""):
  return os.path.join("scripts", fname)

def path4result(fname=""):
  return os.path.join("results", fname)


## The actual dependencies for the thesis.
intro = []

## Raw figures and files
##
## SCons parses the files itself and so misses the files dependencies
## because we use subinputfrom and subincludefrom.

for f in ['beads-on-a-string.png']:
  intro.append(env.File(path4figure(f)))

intro.append(env.File("intro.tex"))

##
## Figures to be created
##

intro.append(env.PDF_TEX(path4figure('chromatin-hierarchy-diagram.svg')))


##
## The scripts
##

nucleosome_views_script = env.File(path4script("nucleosome-views.py"))

nucleosome_views = {
  'H2A' : 'nucleosome-h2a',
  'H2B' : 'nucleosome-h2b',
  'H3'  : 'nucleosome-h3',
  'H4'  : 'nucleosome-h4',
  'H2A-H2B' : 'nucleosome-h2a-h2b-dimer',
  'H3-H4'   : 'nucleosome-h3-h4-dimer',
  'nucleosome-top' : 'nucleosome-top',
  'nucleosome-side' : 'nucleosome-side',
  'octamer-side' : 'octamer-side',
  'tetramer-side' : 'tetramer-side',
}

nucleosome_2cv5 = path4data('2cv5.pdb')
for view, fname in nucleosome_views.iteritems():
  target = path4result(fname + '.png')
  view_fig = env.Command(target=target, source=nucleosome_2cv5,
                         SCRIPT=nucleosome_views_script,
                         action='pymol -cq $SCRIPT -- $SOURCE %s $TARGET' % view)
  env.Depends(view_fig, [nucleosome_views_script])
  intro.append(view_fig)

## Note quite the right thing.  In this case there is no source (well,
## SOURCE is not different from SCRIPT).
sec_struct_script = env.File(path4script('histone-secondary-structure.py'))
sec_struct = env.PythonOutput(
  script=sec_struct_script,
  target=path4result('histone-secondary-structures.tex'),
  source=sec_struct_script)

env.Depends(sec_struct, [sec_struct_script])
intro.append(sec_struct)


##
## "Configure" - check that we have all the required tools installed
##

def CheckProg(context, prog_name):
  context.Message("Checking for %s..." % prog_name)
  is_ok = context.env.WhereIs(prog_name)
  context.Result(is_ok)
  return is_ok

conf = Configure (
  env,
  custom_tests = {
    "CheckProg" : CheckProg,
  },
)

## How the fuck is this not the default in SCons?
if not env.GetOption('help'):
  progs = {
    "pymol"
      : "pymol must be installed to draw nucleosome structure",
  }

  for p_name, p_desc in progs.iteritems():
    if not conf.CheckProg(p_name):
      print p_desc
      Exit(1)

env = conf.Finish()

Return (['intro'])
